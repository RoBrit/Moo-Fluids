/*
 * build.gradle
 *
 * Copyright (c) 2014-2017 TheRoBrit
 *
 * Moo-Fluids is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Moo-Fluids is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

// Add plugins
apply plugin: "net.minecraftforge.gradle.forge"
//apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "maven-publish"

// Add scripts
apply from: "gradle/scripts/dependencies.gradle"
apply from: "gradle/scripts/builds.gradle"
apply from: "gradle/scripts/sign.gradle"
//apply from: "gradle/scripts/curseforge.gradle"

// Java version
sourceCompatibility = 1.8 // Sourcecode Java version
targetCompatibility = 1.8 // Targeted Java version

// Project information
archivesBaseName = mod_name // Mod Name
group = mod_package // Mod Package
version = "${minecraft_version}-${mod_version}" // Compiled Mod Version

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    useDepAts = true // Required for JEI

    mappings = mcp_mappings

    replace "@MOD_ID@", mod_id
    replace "@MOD_NAME@", mod_name
    replace "@MOD_VERSION@", project.version
    replace "@MOD_PACKAGE@", mod_package

    replaceIn "ModInformation.java"
}

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }

        maven {
            name = "gradle"
            url "https://plugins.gradle.org/m2/"
        }

        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
//        classpath "com.matthewprenger:CurseGradle:1.0.7"
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }

        resources {
            srcDir 'src/main/resources'
        }
    }
}

processResources {
    inputs.property "mod_id", mod_id
    inputs.property "mod_name", mod_name
    inputs.property "mod_version", project.version
    inputs.property "mc_version", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "*.info"

        expand "mod_id": mod_id,
               "mod_name": mod_name,
               "mod_version": project.version,
               "mc_version": project.minecraft.version
    }

    // copy everything else, that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "**/*.info"
    }
}